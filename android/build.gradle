apply plugin: 'com.android.application'
apply plugin: 'composer'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.triplet.play'

repositories {
    jcenter()
    mavenLocal()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.google.com' }
}

android {

    compileSdkVersion 27

    defaultConfig {
        versionCode 347
        versionName "3.4.7"
        minSdkVersion 14
        targetSdkVersion 27
        applicationId "org.ligi.passandroid"
        testInstrumentationRunner "org.ligi.passandroid.AppReplacingRunner"
        archivesBaseName = "PassAndroid-$versionName"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "maps", "analytics", "distribution"

    productFlavors {

        withMaps {
            dimension "maps"
        }

        noMaps {
            dimension "maps"
        }

        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        forFDroid {
            dimension "distribution"
        }

        forPlay {
            dimension "distribution"
        }
        forAmazon {
            dimension "distribution"
        }

    }
    android.variantFilter { variant ->
        def maps = variant.getFlavors().get(0).name
        def analytics = variant.getFlavors().get(1).name
        def distribution = variant.getFlavors().get(2).name

        variant.setIgnore((project.hasProperty("singleFlavor") && (distribution != 'forPlay'))
                || ((distribution == 'forAmazon' || distribution == 'forPlay') && analytics == 'noAnalytics')
                || ((distribution == 'forAmazon' || distribution == 'forPlay') && maps == 'noMaps')
                || (distribution == 'forFDroid' && analytics == 'withAnalytics')
                || (distribution == 'forFDroid' && maps == 'withMaps'))
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'

    }

    lintOptions {
        warning 'MissingTranslation'
        warning 'InvalidPackage'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
}

dependencies {
    implementation 'com.github.hotchemi:permissionsdispatcher:2.3.2'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.3.2'

    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'

    provided 'org.glassfish:javax.annotation:10.0-b28'

    androidTestImplementation 'com.github.ligi:trulesk:0.23'

    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'

    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.1'

    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.1'

    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'

    androidTestImplementation "com.android.support:appcompat-v7:$support_version"
    androidTestImplementation "com.android.support:design:$support_version"

    implementation 'com.github.ligi:TouchImageView:2.1'
    implementation 'com.github.ligi:ExtraCompats:0.4'

    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v7:$support_version"

    implementation 'net.i2p.android.ext:floatingactionbutton:1.10.1'

    implementation 'com.github.ligi:KAXT:0.20'
    implementation 'com.github.ligi:KAXTUI:0.4'
    implementation 'com.github.ligi:loadtoast:1.10.11'
    implementation 'com.github.ligi:tracedroid:1.4'

    forPlayImplementation 'com.github.ligi.snackengage:snackengage-playrate:0.15'
    forFDroidImplementation 'com.github.ligi.snackengage:snackengage-playrate:0.15'
    forAmazonImplementation 'com.github.ligi.snackengage:snackengage-amazonrate:0.15'

    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.moshi:moshi:1.4.0'

    implementation "com.chibatching.kotpref:kotpref:2.5.0"
    implementation "com.chibatching.kotpref:initializer:2.5.0"

    testImplementation "com.android.support:support-annotations:$support_version"
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation 'org.threeten:threetenbp:1.3.7'

    // https://github.com/ligi/PassAndroid/issues/181
    // Don't update to 3.3.1 before minSDK 19 - or replace zxing
    implementation 'com.google.zxing:core:3.3.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'

    withAnalyticsImplementation "com.google.android.gms:play-services-analytics:$play_version"
    withMapsImplementation "com.google.android.gms:play-services-maps:$play_version"
}

play {
    jsonFile = file('/media/ligi/USBCRED/play.json')
    uploadImages = true
}
